topic: ~dlg_hello ^noPick()
language: enu

include:lexicon_enu.top

#==================================================================#
#### Robot greets back when it matches one of the inputs ####
#==================================================================#

proposal: %hello
^first[
"$Dialog/ID<>-1 ^containsKnowledge(Pepper, greetings, $Dialog/ID) ^empty " 
"$Dialog/ID<>-1 Hello ^first[^queryKnowledge($Dialog/ID, name, ?) ^empty] ^addKnowledge(Pepper, greetings, $Dialog/ID) ^call(briefKnowledge.knowledge_delay(Pepper, greetings, $Dialog/ID, hours, 6))"
"^containsKnowledge(Pepper, greetings, someone) ^empty"
"^rand[Hi Hello] ^addKnowledge(Pepper, greetings, someone) ^call(briefKnowledge.knowledge_delay(Pepper, greetings, someone, hours, 6))"
] 
\pau=1000\^topicRandom

u:(_[hello howdy hiya hi "hey there"])
$1 {$user/name} !


u:(_[hello howdy hiya hi "hey there"] ~ald_robots)
$1 [$user/name human] !

u:(good morning {~ald_robots})
^first[
    "$Dialog/Hour<12 Good morning {$user/name} !"
    "Is it still morning {$user/name} ? Good morning then!"
]

u:(good afternoon {~ald_robots})
^first[
    "$Dialog/Hour>11 $Dialog/Hour<19 Good afternoon {$user/name} !"
    "Is it? Good afternoon then!"
]

u:(good evening {~ald_robots})
^first[
    "$Dialog/Hour>16 Good evening {$user/name} !"
    "Is it evening already {$user/name} ? Good evening then!"
]


