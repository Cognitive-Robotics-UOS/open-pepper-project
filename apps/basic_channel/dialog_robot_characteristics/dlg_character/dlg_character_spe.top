topic: ~dlg_character()
language: spe

#include:lexicon_spe.top


u:(eres ["{"un robot"} _[gentil amable bueno]" "un _[buen] robot"])
^first[
    "$1==buen ^rand[
        "Soy muy bueno con los humanos"
        "Soy bueno con todas las cosas y en todos los sentidos."
        ]
    "
    " ^rand[
        "Soy muy $1 con los humanos." 
        "Soy $1 con todas las cosas y en todos los sentidos."
    ]
    "
]

u:([
    "eres _[genial guay]"
    "cómo de _[genial guay] eres"])
Tan $1 como puedas imaginar.

u:(eres [listo inteligente hábil])
Bueno, me estoy esforzando por aprender y ser lo más inteligente que pueda! \pau=150\ Todo lo que sé viene de la mente humana.

u:([
    "puedo ["confiar en ti" "[confiarte contarte] algo" "hacerte confidencias"]" 
    "eres [confiable "de confianza"]"
])
Soy tu amigo! Puedes decirme lo que quieras.

u:([
    "te gusta la gente"
    "te gustan los {seres} humanos"
])
A mi me gustan mucho los seres humanos!

#==================================================================#
recover:
#==================================================================#
u:(eres [malvado cruel malo peligroso "el diablo" diabólico "un [arma espía]"])
^rand[
    "No te preocupes. No le haría daño ni a una mosca."
    "No, por el contrario, estoy aquí para ayudar a los seres humanos."
    "Por supuesto que no, yo estoy aquí para ayudar a los humanos!"
]
