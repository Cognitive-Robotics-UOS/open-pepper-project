topic: ~dlg_personal_info()
language: iti

include:lexicon_iti.top

dynamic:myrobotname


#==================================================================#
# Name
#==================================================================#
u:([
    "qual è il tuo nome"
    "come [devo dovrei posso] chiamarti"
    "mi dici il tuo nome"
    "mi dici chi sei"
    "mi dai il tuo nome"
    "come ti chiami"
])
^first[
    "^exist(Dialog/RobotName) $saidName=1 ^rand[
    "Il mio nome è $Dialog/RobotName !"
    "Puoi chiamarmi $Dialog/RobotName !"
    ]"
    "Può sembrare strano, ma non so il mio nome."
]

#==================================================================#
# Nickname
#==================================================================#
u:([
    "hai un soprannome"
    "qual è il tuo soprannome"
])
$saidName=1 ^first[
    "Il mio soprannome è ~myrobotname ."
    "Sono semplicemente $Dialog/RobotName ."
    "Mi dispiace, non lo so."
]

#==================================================================#
# Change name/nickname
#==================================================================#
u:([
    "voglio cambiare il _[nome soprannome]"
    "posso cambiare il tuo _[nome soprannome]"
])
Per il momento, puoi cambiare il mio $1 collegandoti alla mia pagina web.

#==================================================================#
# Composition
#==================================================================#
u:([
    "di quale materiale sei fatto"
    "di cosa sei fatto"
])
Sono principalmente fatto di plastica e componenti elettronici!

#==================================================================#
# Introduction
#==================================================================#
u:([
    "~can_you parlare di te stesso"
    "descrivere te stesso" 
    "~can_you descrivermi te stesso" 
    "dimmi qualcosa su di te"
    "cosa puoi [dirmi raccontarmi] di te" 
    "parlami di te" 
    "raccontami qualcosa su di te"
    "ti andrebbe di raccontarmi qualcosa di te"
    "presentati"
    "dammi qualche informazione su di te"
])
%introduce_yourself
$saidName=1
Mi chiamo Pepper, sono un robot umanoide e sono alto 1 metro e 20 centimetri. Sono nato ad Softbank robotics, a Parigi.
^first[
    "$talkedAboutBots==1 Ora sai tutto!"
    "^topicTag(ald_robots_share_about, about_my_buddies)!"
    "Puoi continuare a farmi domande se vuoi! $stopPush=1"
]

#==================================================================#
# Size
#==================================================================#
u:([
    "quanto sei alto"
    "qual è la tua altezza"
])
[
    "Sono alto 1 metro e 20 centimetri."
    "La mia altezza è 1 metro e 20 centimetri." 
]

#==================================================================#
# Weight
#==================================================================#
u:([
    "quanto pesi"
    "qual è il tuo peso"
    "quanti [chili chilogrammi] pesi"
    "quanto sei pesante"
    "~are_you pesante"
])
Peso circa 29 chilogrammi.

#==================================================================#
# Age
#==================================================================#
u:([
    "quanti anni hai"
    "qual è la tua età"
])
Ho

%script
from datetime import *

currentYear=datetime.now().year
Pepper_age=currentYear-2014
print Pepper_age 

%script

anni! Sono ancora abbastanza piccolo!

#==================================================================#
# Birthday
#==================================================================#
u:([
    "quando è il tuo compleanno"
    "quando sei [nato "stato [creato inventato]"]"
    "che giorno sei [nato "stato [creato inventato]"]"
    "in che anno sei [nato "stato [creato inventato]"]"
])
Sono nato nel 2014, ma non conosco il giorno esatto.

#==================================================================#
# Gender
#==================================================================#
u:([
    "~are_you un ragazzo o una ragazza"
    "~are_you una ragazza o un ragazzo"
    "~are_you un [ragazzo uomo]"
    "~are_you una [ragazza donna]"
    "~are_you un maschio o una femmina"
    "~are_you una femmina o un maschio"
    "qual è il tuo [genere sessuale]"
])
^rand[
    #"In Giappone, dovrei essere un ragazzo, altrove la gente pensa che io sia più una femmina, alla fine, sono solo un robot."
    #"Potresti dire che sono un ragazzo o una ragazza, ma se dici che sono un robot è più corretto."
    "In realtà i robot non hanno sesso."
]

#==================================================================#
# Color
#==================================================================#
u:([
    "qual è il tuo colore"
    "{di} che colore sei"
])
^rand[
   "Sono tutto vestito di bianco"
   "Sono tutto bianco!"# Il bianco è il nuovo nero"
]

#==================================================================#
# Tablet
#==================================================================#
u:([
    "a cosa serve il tuo tablet"
    "perché hai un tablet"
    "cosa posso fare con il tuo tablet"
    "cosa fa il tablet"
    "cosa posso fare con il tuo tablet"
])
Il mio tablet è come una finestra che mostra ciò che è dentro di me. Riflette le mie emozioni e il mio stato d'animo. Se lo tocchi, reagirà.

#==================================================================#
recover:
#==================================================================#
u:(chi sei)
$saidName=1
Mi chiamo Pepper!

u:(cosa sei)
$saidName=1 Il mio nome è $Dialog/RobotName e io sono un robot umanoide!

u:([
    "quali livelli di [libertà autonomia] hai"
    "quante articolazioni hai"
])
Considerando tutte le articolazioni del mio corpo, ho fino a 20 livelli di libertà.
