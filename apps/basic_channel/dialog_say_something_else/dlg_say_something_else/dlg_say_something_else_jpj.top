topic: ~dlg_say_something_else()
language: jpj

include:lexicon_jpj.top

#==================================================================#
##### Makes the robot switch to another topic #####
#==================================================================#

u:^private(e:Dialog/NothingToSay)
^rand[
    "申し訳ないですけど、あんまりたいしたことは思いつきません。"
    "今のところ話せる話題は全部話してしまいました！"
] ^releaseFocus

#ALD/SB ok
u:([なぜ どうして　なんで　なして] [話す　はなしする しゃべる]の{を}[やめた やめる]) 
^rand[
    "あなたの声にうっとりしてしまったもので、つい。気を取り直して続けましょう！"
    "\vct=130\あ！失礼しました！\vct=100\いや、なんの話をしたらいいかなあと思って。そうそう、"
] ^topicRandom

u:(["もう話さないの" "{もう}何も言わなくなった"]) 
^rand[
    "すいません、ちょっと待ってください。" 
    "いえ、何話そうかと考えてたんです。えっとですね。" 
    "そうですね、何か話さないと。"
] ^topicRandom

u:([
    "[話題 話]{を}[変えて 変えよう　変えましょう]"
    "[その この][話 話題]{は}やめよう"
    "[その事に　この事に　これに　それに][ついて　関して][話す はなしする]の{は}[やめよう やめて]"
    "[それは あんまり　そんなに　あんまし　そこまで　それほど　たいして　あんま　全然　まったく]興味ない"
    "[別の 違う]こと{について} [話して 話そう　話さない　しゃべって　しゃべろう　しゃべりましょう]"
    "[その事に　この事に　これに　それに][ついて　関して]{は}[話したくない はなししたくない]"
    "もっと話そう"
    "[それについては そのことは]{あんまり　あんまし　そんなに}話したくない"
    "[違う 別の]{内容の　話題}[話して はなしして]"
])
^rand[
    "わかりました。何か他のことについて話しましょう。" 
    "では話題を変えましょう。" 
    "そうですか、では違う話をしましょう。"
] ^topicRandom

u:([
    "次の質問" 
    "次{に}[行こう 行きましょう 行って]"
]) 
わかりました。では次行きましょう。^topicRandom


#==================================================================#
##### Ask the robot for a new subject #####
#==================================================================#
u:([
    "[なに　どんな  どういう　どのような　どういった　なんの]話{を}[したらいい すればいい]"
    "[どんな  どういう　どのような　どういった　なんの]{内容の}[話 話題]が[ある　ありますか　出来る 出来ますか]"
]) %topicslist
$size_descriptions="^size(~all_descriptions)"
^first[
    "$size_descriptions==0 よく分からないんですが、簡単な質問をしてください！"
    "いまのところ、^enumerate(~all_descriptions) のことだったら話せます！好きな話題を選んでください！"
]

    u1:(["一番" "最初"])
    オッケー　！ ^interpret($Dialog/Enumerate/1)
    
    u1:([二番目　二番])
    オッケー　！ ^interpret($Dialog/Enumerate/2)
    
    u1:([三番目　三番])
    オッケー　！ ^interpret($Dialog/Enumerate/3)
    
    u1:([最後の　四番目　四番])
    オッケー　！ ^interpret($Dialog/Enumerate/Last)
    
#==================================================================#
recover:
#==================================================================#
u:(話題を変えよう) 
わかりました。何か他のことについて話しましょう。^topicRandom

u:(もしもし)
はい！ ^topicRandom

u:(["何か他の" "他に何か"])
^rand[
    "わかりました。何か他のことについて話しましょう。" 
    "では話題を変えましょう。" 
    "そうですか、では違う話しましょう。"
] ^topicRandom

u:(なんか話して)
はい、お話ですね！ ^topicRandom

u:(なんか言って)
わかりました。^topicRandom
