topic: ~dlg_move_arms()
language: iti

include: lexicon_iti.top


#==================================================================#
# Various move commands the robot can respond to
#==================================================================#

u:^private(e:Dialog/NotUnderstood2)
[
    "Chiedimi di fare movimenti più semplici, non posso muovermi esattamente come fanno gli esseri umani."
    "Non capisco quello che mi stai chiedendo, probabilmente ancora non conosco questo movimento."
]

u:^private([
    "quali movimenti"
    "che tipo di movimenti"
    "cerca di essere più preciso"
    "sii più specifico"
    "aiutami a essere più preciso specifico"
    "come posso essere più specifico"
    "fammi un esempio"
])
^rand[
    "Puoi dirmi di aprire le mani o alzare il braccio, per esempio."
    "Puoi chiedermi di spostare la testa o di muovere le mani."
]

u:^private (~do_it_again)
^first[
    "^exist(RobotLastAnim)
    ^rand["Va bene, lo faccio di nuovo." "Ecco che lo rifaccio!"]
    ^runBehavior($RobotLastAnim)"
    "Penso di non avere nulla da rifare."
] ^stayInScope

u:([
    "voglio che tu ti muova"
    "~can_you muoverti"
    "fa alcuni movimenti"
    "fai i movimenti"
    "fai alcuni movimenti"
    "riesci a muoverti"
    "riesci a fare alcuni movimenti"
])
Sì! ^rand[
    "Puoi dirmi di aprire le mani o alzare il braccio, per esempio."
    "Puoi chiedermi di spostare la testa o di muovere le mani."
] $RobotLastAnim=""

#==================================================================#
# Move - Arms
#==================================================================#

u:(~can_you alzare le braccia)
Quale braccio dovrei alzare?

    u1:([
        "{il quello} sinistro"
        "a sinistra"
        "il braccio sinistro"
    ])
    Così va bene?
    ^runBehavior(dialog_move_arms/animations/UpLArm)
    $RobotLastAnim=dialog_move_arms/animations/UpLArm
    
    u1:([
         "{il quello} destro"
        "a destra"
        "il braccio destro"
    ])
    Ecco qui!
    ^runBehavior(dialog_move_arms/animations/UpRArm)
    $RobotLastAnim=dialog_move_arms/animations/UpRArm

    u1:([
        entrambe
        "sinistro e destro"
        "destro e sinistro"
    ])
    Ed ecco qua!
    ^runBehavior(dialog_move_arms/animations/UpBothArms)
    $RobotLastAnim=dialog_move_arms/animations/UpBothArms
    
     u1:(~its_up_to_you)
    ^rand[
        "Quindi, sarà il sinistro!
        ^runBehavior(dialog_move_arms/animations/UpLArm)
        $RobotLastAnim=dialog_move_arms/animations/UpLArm"
        "Quindi, sarà il destro!
        ^runBehavior(dialog_move_arms/animations/UpRArm)
        $RobotLastAnim=dialog_move_arms/animations/UpRArm"
    ]

u:(~can_you alzare il braccio sinistro)
Sto alzando il braccio sinistro!
^runBehavior(dialog_move_arms/animations/UpLArm)
$RobotLastAnim=dialog_move_arms/animations/UpLArm

u:(~can_you alzare il braccio destro)
Sto alzando il braccio destro!
^runBehavior(dialog_move_arms/animations/UpRArm)
$RobotLastAnim=dialog_move_arms/animations/UpRArm

u:([
    "~can_you [alzare sollevare] {entrambe} le braccia"
    "[alza solleva] {entrambe} le braccia"
])
Ed ecco qua!
^runBehavior(dialog_move_arms/animations/UpBothArms)
$RobotLastAnim=dialog_move_arms/animations/UpBothArms

u:([
    "~can_you [allungare spalancare] le braccia"
    "[allunga spalanca] le braccia"
])
Va bene!
^runBehavior(dialog_move_arms/animations/StretchBothArms)
$RobotLastAnim=dialog_move_arms/animations/StretchBothArms

u:([
    "~can_you [allungare distendere] il braccio sinistro"
    "[allunga distende] il braccio sinistro"
])
Ecco qua!
^runBehavior(dialog_move_arms/animations/StretchLArm)
$RobotLastAnim=dialog_move_arms/animations/StretchLArm

u:([
    "~can_you [allungare distendere] il braccio destro"
    "[allunga distende] il braccio destro"
])
Ecco qua!
^runBehavior(dialog_move_arms/animations/StretchRArm)
$RobotLastAnim=dialog_move_arms/animations/StretchRArm
