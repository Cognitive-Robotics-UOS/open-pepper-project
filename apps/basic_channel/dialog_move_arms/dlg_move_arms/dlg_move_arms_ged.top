topic: ~dlg_move_arms()
language: ged

include: lexicon_ged.top


#==================================================================#
# Various move commands the robot can respond to
#==================================================================#

u:^private(e:Dialog/NotUnderstood2)
[
    "Bitte mich, einfachere Bewegungen durchzuführen, ich kann mich nicht genau so wie ein Mensch bewegen."
    "Ich verstehe nicht, worum du mich bittest, ich kenne diese Bewegung bis jetzt vielleicht noch nicht."
]

u:^private([
    "welche bewegungen"
    "welche art von bewegung"
    "sei bitte genauer"
    "sei bitte konkreter"
    "hilf mir [genauer konkreter] zu sein"
    "wie kann ich konkreter werden"
    "gib mir ein beispiel"
])

^rand[
    "Du kannst mir sagen, ich soll meine Hände öffnen oder den Arm heben, zum Beispiel."
    "Du kannst mich bitten, meinen Kopf zu bewegen oder meine Hände zu bewegen."
]

u:^private (~do_it_again)
^first[
    "^exist(RobotLastAnim)
    ^rand["Okay, ich werde es noch einmal machen." "Jetzt geht es wieder los"]
    ^runBehavior($RobotLastAnim)"
    "Ich glaube, ich habe nichts zum Wiederholen."
] ^stayInScope

#==================================================================#
# Move - Arms
#==================================================================#

u:([
    "{~can_you} deinen Arm heben"
    "hebe deinen Arm"
])
Welchen Arm soll ich heben?

    u1:([
        "den linken Arm"
        "den linken"
        "den links"
        "links"
        "deinen linken Arm"
    ])
    Etwa so?
    ^runBehavior(dialog_move_arms/animations/UpLArm)
    $RobotLastAnim=dialog_move_arms/animations/UpLArm
    
    u1:([
        "den rechten Arm"
        "den rechten"
        "den rechts"
        "rechts"
        "deinen rechten Arm"
    ])
    Bitte sehr!
    ^runBehavior(dialog_move_arms/animations/UpRArm)
    $RobotLastAnim=dialog_move_arms/animations/UpRArm

    u1:([
        "beide"
        "links und rechts"
        "rechts und links"
    ])
    Und schon geht es los!
    ^runBehavior(dialog_move_arms/animations/UpBothArms)
    $RobotLastAnim=dialog_move_arms/animations/UpBothArms
    
     u1:(es liegt ganz bei dir)
    ^rand[
        "Aha, den linken also!
        ^runBehavior(dialog_move_arms/animations/UpLArm)
        $RobotLastAnim=dialog_move_arms/animations/UpLArm"
        "Aha, den rechten also!
        ^runBehavior(dialog_move_arms/animations/UpRArm)
        $RobotLastAnim=dialog_move_arms/animations/UpRArm"
    ]

u:([
    "~can_you deinen linken Arm heben"
    "hebe deinen linken Arm"
])
Ich hebe meinen linken Arm!
^runBehavior(dialog_move_arms/animations/UpLArm)
$RobotLastAnim=dialog_move_arms/animations/UpLArm

u:([
    "~can_you deinen rechten Arm heben"
    "hebe deinen rechten Arm"
])
Ich hebe meinen rechten Arm!
^runBehavior(dialog_move_arms/animations/UpRArm)
$RobotLastAnim=dialog_move_arms/animations/UpRArm

u:([
    "~can_you beide Arme [heben anheben]"
    "hebe deine {beiden} Arme"
])
Und schon geht es los!
^runBehavior(dialog_move_arms/animations/UpBothArms)
$RobotLastAnim=dialog_move_arms/animations/UpBothArms

u:([
    "~can_you deine Arme ausstrecken"
    "~can_you deine Arme weit öffnen"
    "strecke deine Arme aus"
    "öffne deine Arme weit"
])
In Ordnung!
^runBehavior(dialog_move_arms/animations/StretchBothArms)
$RobotLastAnim=dialog_move_arms/animations/StretchBothArms

u:([
    "~can_you deinen linken Arm [ausstrecken öffnen]"
    "[strecke öffne] deinen linken Arm"
])
Bitte sehr!
^runBehavior(dialog_move_arms/animations/StretchLArm)
$RobotLastAnim=dialog_move_arms/animations/StretchLArm

u:([
    "~can_you deinen rechten Arm [strecken öffnen]"
    "[strecke öffne] deinen rechten Arm"
])
Bitte sehr!
^runBehavior(dialog_move_arms/animations/StretchRArm)
$RobotLastAnim=dialog_move_arms/animations/StretchRArm
